# Terraform on Azure

## Vendor documentation for reference

- (Terraform)[https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs]

- (Microsoft)[https://learn.microsoft.com/en-us/azure/developer/terraform/overview]

## Prerequisites

#### "Azure for Students" subscription or your own dev-test subscription in Azure
- https://azure.microsoft.com/en-us/free/students/

#### az-cli
- https://learn.microsoft.com/en-us/cli/azure/install-azure-cli

#### terraform
- https://developer.hashicorp.com/terraform/downloads

---

## Step-by-Step Instructions

- Login with az login, a web browser page will open to facalitate login

```bash
az login
```

- The output will generate a json list of available subscriptions.  Find the entry with "Azure for Students" or chosen subscription:

```bash
  {
    "cloudName": "AzureCloud",
    "homeTenantId": "4cc ... c6d6",
    "id": "96XXXXXX-XXXX-XXXX-XXXX-XXXXXXXXXXab", ### <-- copy this
    "isDefault": false,
    "managedByTenants": [],
    "name": "Azure for Students", ### <-- good subscription
    "state": "Enabled",
    "tenantId": "4cc ... 6d6",
    "user": {
      "name": "me@wustl.edu",
      "type": "user"
    }
  },
```

- Set the subscription for use with terraform and az-cli

```bash
az account set --subscription "96XXXXXX-XXXX-XXXX-XXXX-XXXXXXXXXXab"
```

- (I don't know how to do an alert or box or whatever in .md) Vendor documentation recommends setting a service principal.  This is only necessary for non-interactive activity/CI-CD/Automation, etc.

## Create a VM

Microsoft documentation: https://learn.microsoft.com/en-us/azure/virtual-machines/linux/quick-create-terraform?tabs=azure-cli

- Microsoft recommends the following base files:
  - `main.tf`
  - `outputs.tf`
  - `providers.tf`
  - `ssh.tf`
  - `variables.tf`

- create a working directory and place the files in there

## Files

- edit `main.tf` with the following contents
```bash
resource "random_pet" "rg_name" {
  prefix = var.resource_group_name_prefix
}

resource "azurerm_resource_group" "rg" {
  location = var.resource_group_location
  name     = random_pet.rg_name.id
}

# Create virtual network
resource "azurerm_virtual_network" "my_terraform_network" {
  name                = "myVnet"
  address_space       = ["10.0.0.0/16"]
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
}

# Create subnet
resource "azurerm_subnet" "my_terraform_subnet" {
  name                 = "mySubnet"
  resource_group_name  = azurerm_resource_group.rg.name
  virtual_network_name = azurerm_virtual_network.my_terraform_network.name
  address_prefixes     = ["10.0.1.0/24"]
}

# Create public IPs
resource "azurerm_public_ip" "my_terraform_public_ip" {
  name                = "myPublicIP"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  allocation_method   = "Dynamic"
}

# Create Network Security Group and rule
resource "azurerm_network_security_group" "my_terraform_nsg" {
  name                = "myNetworkSecurityGroup"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name

  security_rule {
    name                       = "SSH"
    priority                   = 1001
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "22"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }
}

# Create network interface
resource "azurerm_network_interface" "my_terraform_nic" {
  name                = "myNIC"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name

  ip_configuration {
    name                          = "my_nic_configuration"
    subnet_id                     = azurerm_subnet.my_terraform_subnet.id
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.my_terraform_public_ip.id
  }
}

# Connect the security group to the network interface
resource "azurerm_network_interface_security_group_association" "example" {
  network_interface_id      = azurerm_network_interface.my_terraform_nic.id
  network_security_group_id = azurerm_network_security_group.my_terraform_nsg.id
}

# Generate random text for a unique storage account name
resource "random_id" "random_id" {
  keepers = {
    # Generate a new ID only when a new resource group is defined
    resource_group = azurerm_resource_group.rg.name
  }

  byte_length = 8
}

# Create storage account for boot diagnostics
resource "azurerm_storage_account" "my_storage_account" {
  name                     = "diag${random_id.random_id.hex}"
  location                 = azurerm_resource_group.rg.location
  resource_group_name      = azurerm_resource_group.rg.name
  account_tier             = "Standard"
  account_replication_type = "LRS"
}

# Create virtual machine
resource "azurerm_linux_virtual_machine" "my_terraform_vm" {
  name                  = "myVM"
  location              = azurerm_resource_group.rg.location
  resource_group_name   = azurerm_resource_group.rg.name
  network_interface_ids = [azurerm_network_interface.my_terraform_nic.id]
  size                  = "Standard_DS1_v2"

  os_disk {
    name                 = "myOsDisk"
    caching              = "ReadWrite"
    storage_account_type = "Premium_LRS"
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "0001-com-ubuntu-server-jammy"
    sku       = "22_04-lts-gen2"
    version   = "latest"
  }

  computer_name  = "hostname"
  admin_username = var.username

  admin_ssh_key {
    username   = var.username
    public_key = jsondecode(azapi_resource_action.ssh_public_key_gen.output).publicKey
  }

  boot_diagnostics {
    storage_account_uri = azurerm_storage_account.my_storage_account.primary_blob_endpoint
  }
}
```

- edit `outputs.tf` with the following contents
```bash
output "resource_group_name" {
  value = azurerm_resource_group.rg.name
}

output "public_ip_address" {
  value = azurerm_linux_virtual_machine.my_terraform_vm.public_ip_address
}
```

- edit `providers.tf` with the following contents
```bash
terraform {
  required_version = ">=0.12"

  required_providers {
    azapi = {
      source  = "azure/azapi"
      version = "~>1.5"
    }
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~>2.0"
    }
    random = {
      source  = "hashicorp/random"
      version = "~>3.0"
    }
  }
}

provider "azurerm" {
  features {}
}
```

- edit `ssh.tf` with the following contents
```bash
resource "random_pet" "ssh_key_name" {
  prefix    = "ssh"
  separator = ""
}

resource "azapi_resource_action" "ssh_public_key_gen" {
  type        = "Microsoft.Compute/sshPublicKeys@2022-11-01"
  resource_id = azapi_resource.ssh_public_key.id
  action      = "generateKeyPair"
  method      = "POST"

  response_export_values = ["publicKey", "privateKey"]
}

resource "azapi_resource" "ssh_public_key" {
  type      = "Microsoft.Compute/sshPublicKeys@2022-11-01"
  name      = random_pet.ssh_key_name.id
  location  = azurerm_resource_group.rg.location
  parent_id = azurerm_resource_group.rg.id
}

output "key_data" {
  value = jsondecode(azapi_resource_action.ssh_public_key_gen.output).publicKey
}
```

- edit `variables.tf` with the following contents
```bash
variable "resource_group_location" {
  type        = string
  default     = "eastus"
  description = "Location of the resource group."
}

variable "resource_group_name_prefix" {
  type        = string
  default     = "rg"
  description = "Prefix of the resource group name that's combined with a random ID so name is unique in your Azure subscription."
}

variable "username" {
  type        = string
  description = "The username for the local account that will be created on the new VM."
  default     = "azureadmin"
}
```

## CLI Steps

- Initialize terraform
```bash
terraform init -upgrade
```

- Generate a plan, output to file (safer for execution)
```bash
terraform plan -out main.tfplan
```

- Execute the plan
```bash
terraform apply main.tfplan
```

## Cleanup

- Plan the destruction
```bash
terraform plan -destroy -out main.destroy.tfplan
```

- Execute order 66
```bash
terraform apply main.destroy.tfplan
```

**Verify resources are gone**

EOF